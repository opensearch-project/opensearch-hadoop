
apply plugin: 'opensearch.hadoop.build.integration'

description = "OpenSearch Storm"

evaluationDependsOn(':opensearch-hadoop-mr')

configurations {
    embedded {
        transitive = false
        canBeResolved = true
    }
    implementation {
        extendsFrom project.configurations.embedded
    }
}

dependencies {
    embedded(project(":opensearch-hadoop-mr"))

    api("org.apache.storm:storm-core:$stormVersion") {
        exclude module: "log4j-slf4j-impl"
    }

    implementation("commons-logging:commons-logging:1.1.1")
    implementation("javax.xml.bind:jaxb-api:2.3.1")
    implementation("commons-codec:commons-codec:1.4")

    testImplementation(project(":test:shared"))

    itestImplementation(project(":test:shared"))
    itestImplementation("com.google.guava:guava:16.0.1")
    itestImplementation("com.twitter:carbonite:1.4.0")

    additionalSources(project(":opensearch-hadoop-mr"))
    javadocSources(project(":opensearch-hadoop-mr"))
}

jar {
    dependsOn(project.configurations.embedded)
    from(project.configurations.embedded.collect { it.isDirectory() ? it : zipTree(it)}) {
        include "org/opensearch/hadoop/**"
        include "opensearch-hadoop-build.properties"
        include "META-INF/services/*"
    }
}

// add clojars repo to pom
publishing {
    publications {
        main {
            getPom().withXml { XmlProvider xml ->
                Node root = xml.asNode()
                Node repositories = root.appendNode('repositories')
                Node repository = repositories.appendNode('repository')
                repository.appendNode('id', 'clojars.org')
                repository.appendNode('url', 'https://clojars.org/repo')
            }
        }
    }
}

tasks.getByName('integrationTest').enabled = false